// ──────────────────────────────────────────────
// Whitespace
// ──────────────────────────────────────────────
WHITESPACE = _{ " " | "\t" | NEWLINE }

// ──────────────────────────────────────────────
// Lexical tokens
// ──────────────────────────────────────────────
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
float      = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
integer    = @{ ASCII_DIGIT+ }
boolean    = @{ "true" | "false" }

primitive = _{ float | integer | boolean | identifier }

// ──────────────────────────────────────────────
// Operators
// ──────────────────────────────────────────────
op_add     = { "+" }
op_sub     = { "-" }
op_mul     = { "*" }
op_div     = { "/" }
op_neg     = { "-" }
op_not     = { "!" }
op_cat     = { "?" }
func_call  = { "(" ~ expressions_comma_separated_list? ~ ")" }
value_ctor = { "{" ~ expressions_comma_separated_list? ~ "}" }
type_ctor  = { "[" ~ expressions_comma_separated_list? ~ "]" }

ops_prefix  = _{ op_neg | op_not }
ops_infix   = _{ op_add | op_sub | op_mul | op_div }
ops_postfix = _{ op_cat | func_call | value_ctor | type_ctor }

// ──────────────────────────────────────────────
// Expression grammar (see ast/expr_builder.rs)
// ──────────────────────────────────────────────
opening_brace =  { "(" }
closing_brace =  { ")" }
atom          = _{ primitive | opening_brace ~ infix_expression ~ closing_brace }

prefix_expression  = _{ ops_prefix* ~ atom }
postfix_expression = _{ prefix_expression ~ ops_postfix* }
infix_expression   = _{ postfix_expression ~ (ops_infix ~ postfix_expression)* }

expression                       =  { infix_expression }
expressions_comma_separated_list = _{ expression ~ ("," ~ expression)* ~ ","? }

file = _{ SOI ~ expression ~ EOI }
